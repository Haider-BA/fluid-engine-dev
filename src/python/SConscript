"""
Copyright (c) 2017 Doyub Kim
"""

Import('env', 'os', 'utils')

script_dir = os.path.dirname(File('SConscript').rfile().abspath)

def from_root_dir(path):
    return os.path.join(*[script_dir, '../../..', path])

def from_build_dir(path):
    return os.path.join(lib_env['BUILDDIR'], path)

lib_env = env.Clone()
lib_env.Append(CPPPATH = [
    os.path.join(script_dir, '../../include'),
    os.path.join(script_dir, '../../external/include'),
    lib_env['PYTHONCPPPATH'],
    script_dir])
if 'PYTHONLIBS' in lib_env:
    lib_env.Append(LIBS = [lib_env['PYTHONLIBS']])
if 'PYTHONLINKFLAGS' in lib_env:
    lib_env.Append(LINKFLAGS = [lib_env['PYTHONLINKFLAGS']])
lib_env['SHLIBPREFIX'] = ''
if utils.is_unix():
    lib_env['SHLIBSUFFIX'] = '.so'
elif utils.is_windows():
    lib_env['SHLIBSUFFIX'] = '.pyd'

lib_env.Append(LIBPATH = [from_build_dir('src/jet'), from_build_dir('external/src/obj')])
lib_env.Prepend(LIBS = ['jet', 'obj'])

source_patterns = ['*.cpp']
source = map(lambda x: os.path.relpath(x, script_dir), utils.get_all_files(script_dir, source_patterns))
source = [s for s in source if not (s.endswith('pch.cpp'))]

lib = lib_env.SharedLibrary('pyjet', source)

Return('lib_env', 'lib')

